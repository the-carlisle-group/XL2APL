 GetSheetData←{
     ⍝ ⍺ ←→ Workbook info space
     ⍝ ⍵ ←→ sheet name
     ⍺.RowsProcessed←0
     sn←⍵{0=≢⍺:⊃⍵ ⋄ ⍺}⍺.SheetNames
     i←⍺.SheetNames⍳⊂sn
     f←i⊃⍺.SheetFileNames
     z←OpenExcelFile ⍺.FileName
     _←⍺ GetSharedStrings z
     p←⎕NS''
     p.WorkbookInfo←⍺
     p.SheetName←sn
     p.File←z OpenZipFile f
     p.BlockSize←⍺.BlockSize
     p.Buffer←CreateBuffer p.BlockSize
     p.FirstRow←⍬
     p.Fragment←''
     p.Blocks←0
     p.EndOfRows←0
     p.(Data Rows Columns Header)←⊂⍬
     _←ProcessBlocks p
     _←Dispose¨z p.File
     p.UniqueColumns←{⍵[⍋⍵]}∪∊p.Columns
     _←GetColumnFormatCodes p
     1=p.Blocks:p
     p.Data←p AssembleColumn¨p.UniqueColumns
     p.Header←(p.UniqueColumns∊↑p.Columns)\p.Header
     p
 }
