 GetWorkbookInfo←{
     ⍝ ⍵ ←→ FQP of .xlsx file
     ⍝   ←→ Space of usefull stuff
     a←OpenExcelFile ⍵
     p←⎕NS''
     p.FileName←⍵
     get←{0=≢⍵:'' ⋄ ⍺∘{(⍵[;0]⍳⊂⍺)⊃⍵[;1],⊂''}¨⍵}
     r←⎕XML a ReadZipFile'xl/_rels/workbook.xml.rels'
     ri←'Id'get r[;3]
     rt←'Target'get r[;3]
     m←⎕XML a ReadZipFile'xl/workbook.xml'
     at←m[⍸m[;1]∊⊂'sheet';3]
     p.SheetNames←'name'get at
     p.ActiveTab←⊃1⊃⎕VFI⊃'activeTab'get m[m[;1]⍳⊂'workbookView';3]
     p.ActiveSheet←p.ActiveTab⊃p.SheetNames
     id←'r:id'get at
     fn←'xl/'∘,¨rt[ri⍳id]
     p.SheetFileNames←fn
     k←⍸m[;1]∊⊂'definedName'
     p.RangeNames←'name'get m[k;3]
     p.RangeAddresses←m[k;2]
     _←a GetFormatCodes p
     d←('/'∊⍕⌷a.Entries)⊃'\/'
     f←d@('/'∘=)
     se←{⍵[⍵.FullName⍳f¨fn]}⌷a.Entries
     ⎕USING←'System'
     p.Lengths←Convert.ToDouble¨se.Length
     p.CompressedLengths←Convert.ToDouble¨se.CompressedLength
     p.BlockSize←2*23
     p.HeaderRows←1
     p.RowsToRead←0
     p.SkipBeforeHeader←0
     p.SkipBeforeData←0
     p.OmitEmptyRows←0
     p.OmitEmptyColumns←0
     _←Dispose a
     p
 }
